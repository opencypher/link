subprojects {
    apply plugin: 'maven-publish'

    ext {
        pub = [dev : [artifacts: ['jar',
                                  'sourceJar',]],
               full: [artifacts: ['jar',
                                  'sourceJar',
                                  'docJar',
                                  'testJar',]],
        ]
        pomConfig = {
            name = project.description
            description = project.description
            url = 'https://www.opencypher.org'
            licenses {
                license {
                    name = 'Apache License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0'
                }
            }
            developers {
                developer {
                    id = 'link'
                    name = 'The Link team'
                    email = 'opencypher@neo4j.com'
                    url = 'https://www.opencypher.org'
                }
            }
            scm {
                url = 'https://github.com/opencypher/link'
            }
        }
    }

    publishing {

        repositories {
            maven {
                name = 'buildDir'
                url = "file://${rootProject.file(cfg.publishDir)}"
            }
        }


        publications {
            // We do not want to have dev and full publications for okapi-shade
            if (project.name != "okapi-shade") {
                dev(MavenPublication) {
                    from components.java

                    afterEvaluate {
                        pom pomConfig
                        artifacts = pub.dev.artifacts
                                .findResults { tasks.findByName(it) }
                                .findAll { it.enabled }
                    }
                }

                full(MavenPublication) {
                    from components.java

                    afterEvaluate {
                        pom pomConfig
                        artifacts = pub.full.artifacts
                                .findResults { tasks.findByName(it) }
                                .findAll { it.enabled }
                    }
                }
            }

            // This publication is added only to get a `generatePomFileForJarPublication` task.
            // We use that task to generate a pom.xml to be consumed by the Jar task.
            // Without the pom.xml file our regular Jars, dependency resolution breaks when we shade them.
            jar(MavenPublication) {
                from components.java
            }
        }
    }

    jar {
        from generatePomFileForJarPublication {
            rename('pom-default.xml', "META-INF/maven/${project.group}/${project.name}/pom.xml")
        }
    }

    if (project.name != "okapi-shade") {
        // Convenience for quick publish to maven local
        task devPublish {
            group 'publishing'
            description ' Publishes main jars to the local Maven repository.'
            dependsOn tasks.publishDevPublicationToMavenLocal
        }
    }

    // Task run by teamcity
    task ci {
        dependsOn tasks.check
        dependsOn { tasks.publishFullPublicationToBuildDirRepository }
    }

}
